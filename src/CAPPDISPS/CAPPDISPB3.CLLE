
/*                                                                           +
     (c) 2019 John Erps                                                      +
     This software is licensed under MIT (see LICENSE)                       +
                                                                             +
                                                                             +
     BUILD MODULES                                                           +
                                                                             +
                                                                             +
     Data area QTEMP/CO_TGTRLS (10A), if present, contains the target rls.   +
*/

             PGM        PARM(&TGTLIB &SRCLIB)
             DCL        VAR(&TGTLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SRCLIB) TYPE(*CHAR) LEN(10)

             DCL        VAR(&SRCFILE) TYPE(*CHAR) LEN(10) +
                          VALUE(CAPPDISPS)
             DCL        VAR(&SRCBIND) TYPE(*CHAR) LEN(10) +
                          VALUE(CAPP_DISP)

             DCL        VAR(&TLIB) TYPE(*CHAR) LEN(10) VALUE(' ')
             DCL        VAR(&SLIB) TYPE(*CHAR) LEN(10) VALUE(' ')
             DCL        VAR(&SPTEXT) TYPE(*CHAR) LEN(50)

             DCL        VAR(&TGTRLS) TYPE(*CHAR) LEN(10)

             DCL        VAR(&ERRMSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ERRMSGDTA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&ERRMSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRMSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRORSW) TYPE(*LGL)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))


             CHGVAR     VAR(&TLIB) VALUE(&TGTLIB)
             CHGVAR     VAR(&SLIB) VALUE(&SRCLIB)
             MONMSG     MSGID(MCH3601)
             IF         COND(&SLIB *EQ ' ') THEN(CHGVAR VAR(&SLIB) +
                          VALUE(&TLIB))

             CHGVAR     VAR(&TGTRLS) VALUE('*CURRENT')
             CHKOBJ     OBJ(QTEMP/CO_TGTRLS) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(NOTGTRLS))
             RTVDTAARA  DTAARA(QTEMP/CO_TGTRLS *ALL) RTNVAR(&TGTRLS)
NOTGTRLS:

             /*----- BUILD BEGIN -----*/

             RTVMBRD    FILE(&SLIB/&SRCFILE) MBR(&SRCBIND) +
                          TEXT(&SPTEXT)

             CRTSRVPGM  SRVPGM(&TLIB/CAPPDISP) MODULE(CAPPDISPSI +
                          CAPPDISPHD CAPPDISPXX CAPPDISPRM) +
                          SRCFILE(&SLIB/&SRCFILE) SRCMBR(&SRCBIND) +
                          TEXT(&SPTEXT) ACTGRP(*CALLER) TGTRLS(&TGTRLS)

             /*----- BUILD END -----*/

             RETURN


 ERROR:      IF         COND(&ERRORSW) THEN(SNDPGMMSG MSGID(CPF9999) +
                          MSGF(QCPFMSG) MSGTYPE(*ESCAPE))
             CHGVAR     VAR(&ERRORSW) VALUE('1')
 ERROR2:     RCVMSG     MSGTYPE(*DIAG) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             IF         COND(&ERRMSGID = ' ') THEN(GOTO CMDLBL(ERROR3))
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*DIAG)
             GOTO       CMDLBL(ERROR2)
 ERROR3:     RCVMSG     MSGTYPE(*EXCP) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*ESCAPE)

             ENDPGM

