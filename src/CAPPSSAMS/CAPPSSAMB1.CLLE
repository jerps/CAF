
/*                                                                           +
     (c) 2019 John Erps                                                      +
     This software is licensed under the MIT license (see LICENSE)           +
                                                                             +
                                                                             +
     BUILD PREREQS                                                           +
                                                                             +
*/


             PGM        PARM(&TGTLIB &SRCLIB)
             DCL        VAR(&TGTLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SRCLIB) TYPE(*CHAR) LEN(10)

             DCL        VAR(&SRCFILE) TYPE(*CHAR) LEN(10) +
                          VALUE(CAPPSSAMS)

             DCL        VAR(&TLIB) TYPE(*CHAR) LEN(10) VALUE(' ')
             DCL        VAR(&SLIB) TYPE(*CHAR) LEN(10) VALUE(' ')
             DCL        VAR(&USR) TYPE(*CHAR) LEN(10)

             DCL        VAR(&INID0) TYPE(*LGL)
             DCL        VAR(&INIF3) TYPE(*LGL)
             DCL        VAR(&INIF4) TYPE(*LGL)

             DCL        VAR(&ERRMSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ERRMSGDTA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&ERRMSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRMSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRORSW) TYPE(*LGL)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))


             CHGVAR     VAR(&TLIB) VALUE(&TGTLIB)
             CHGVAR     VAR(&SLIB) VALUE(&SRCLIB)
             MONMSG     MSGID(MCH3601)
             IF         COND(&SLIB *EQ ' ') THEN(CHGVAR VAR(&SLIB) +
                          VALUE(&TLIB))

             /*----- BUILD BEGIN -----*/

             /* FIELD REFERENCE FILE */
             CO         SRCFILE(&SLIB/&SRCFILE) SRCMBR(CAPPSSAMFR) +
                          TGTLIB(&TLIB)

             CHGVAR     VAR(&INID0) VALUE('0')
             CHGVAR     VAR(&INIF3) VALUE('0')
             CHGVAR     VAR(&INIF4) VALUE('0')

             /* DATA AREA CAPPSSAMA0 */
             CHKOBJ     OBJ(&TLIB/CAPPSSAMA0) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CHGVAR     VAR(&INID0) VALUE('1')
             CRTDTAARA  DTAARA(&TLIB/CAPPSSAMA0) TYPE(*CHAR) LEN(50) +
                          VALUE('0000000000000000  0') +
                          TEXT('CAPPSSAM/DataArea 0: Sync Global +
                          Menu Data')
             RTVJOBA    USER(&USR)
             CHGDTAARA  DTAARA(&TLIB/CAPPSSAMA0 (21 10)) VALUE(&USR)
             ENDDO

             /* FILE CAPPSSAMF1 */
             CO         SRCFILE(&SLIB/&SRCFILE) SRCMBR(CAPPSSAMF1) +
                          TGTLIB(&TLIB)

             /* FILE CAPPSSAMF2 */
             CO         SRCFILE(&SLIB/&SRCFILE) SRCMBR(CAPPSSAMF2) +
                          TGTLIB(&TLIB)

             /* FILE CAPPSSAMF3 */
             CHKOBJ     OBJ(&TLIB/CAPPSSAMF3) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&INIF3) +
                          VALUE('1'))
             CO         SRCFILE(&SLIB/&SRCFILE) SRCMBR(CAPPSSAMF3) +
                          TGTLIB(&TLIB)

             /* FILE CAPPSSAMF4 */
             CHKOBJ     OBJ(&TLIB/CAPPSSAMF4) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&INIF4) +
                          VALUE('1'))
             CO         SRCFILE(&SLIB/&SRCFILE) SRCMBR(CAPPSSAMF4) +
                          TGTLIB(&TLIB)

             /* FILE CAPPSSAMF5 */
             CO         SRCFILE(&SLIB/&SRCFILE) SRCMBR(CAPPSSAMF5) +
                          TGTLIB(&TLIB)

             /* FILE CAPPSSAMF6 */
             CO         SRCFILE(&SLIB/&SRCFILE) SRCMBR(CAPPSSAMF6) +
                          TGTLIB(&TLIB)

             /* PGM CAPPSSAMP0 */
             CO         SRCFILE(&SLIB/&SRCFILE) SRCMBR(CAPPSSAMP0) +
                          TGTLIB(&TLIB)

             /* INITIAL DATA LOAD */
             CALL       PGM(CAPPSSAMP0) PARM(&INID0 &INIF3 &INIF4)

             /*----- BUILD END -----*/

             RETURN


 ERROR:      IF         COND(&ERRORSW) THEN(SNDPGMMSG MSGID(CPF9999) +
                          MSGF(QCPFMSG) MSGTYPE(*ESCAPE))
             CHGVAR     VAR(&ERRORSW) VALUE('1')
 ERROR2:     RCVMSG     MSGTYPE(*DIAG) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             IF         COND(&ERRMSGID = ' ') THEN(GOTO CMDLBL(ERROR3))
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*DIAG)
             GOTO       CMDLBL(ERROR2)
 ERROR3:     RCVMSG     MSGTYPE(*EXCP) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*ESCAPE)

             ENDPGM

