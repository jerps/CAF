
      *  (c) 2019 John Erps
      *  This software is licensed under the MIT license (see LICENSE)


      *  File CAPPSSAMF1

      *  Service Program: Common System Services / SubSystem AppMenu
      *  File:          : Slots


      *  Attributes:

      *   MENUSBIN   - Bin (0 or 1)
      *                Position 19 in data area CAPPSSAMD0 contains
      *                the "bin" with the current slots, which is
      *                either "0" or "1". Records within the other
      *                bin (0 or 1) should be ignored because these
      *                are from the previous menu version. This "other"
      *                bin can be used as a back-up, but with caution
      *                because these records are incomplete and/or
      *                corrupt if the last save operation did not
      *                succeed.

      *   MENUSPAGE  - Page (0-999)
      *                If this field contains 0 then this record applies to
      *                the menu, else this record applies to page MENUSPAGE
      *                (MENUSLOT=0), or an option or a header.

      *   MENUSLOT   - Slot (0-45)
      *                      1-15 : left strip
      *                     16-30 : middle strip
      *                     31-45 : right strip
      *                Ignore this field if MENUSPAGE is 0.
      *                The record applies to page MENUSPAGE if this field
      *                contains 0 and MENUSPAGE is greater than 0.
      *                The record applies to an individual option or header
      *                contained in slot MENUSLOT on page MENUSPAGE if this
      *                field is greater than 0 and MENUSPAGE is greater than 0.

      *   MENUSCHC   - Choice (any value)
      *                Ignore this field if MENUSPAGE or MENUSLOT is 0.
      *                The record applies to an option if MENUSPAGE and MENUSLOT
      *                are both greater than 0 and this field is NOT blank.
      *                The record applies to a header if MENUSPAGE and MENUSLOT
      *                are both greater than 0 and this field is blank. In this
      *                case all fields relevant to an option should be ignored,
      *                except MENUSTEXT.

      *   MENUSTEXT  - Menu or page title, option or header text.
      *                Contains the menu title if MENUSPAGE is 0.
      *                Contains the title for page MENUSPAGE if MENUSPAGE is
      *                greater than 0 and MENUSLOT is 0.
      *                Contains the option text if MENUSPAGE and MENUSLOT are
      *                both greater than zero and MENUSCHC is NOT blank.
      *                Contains the header text if MENUSPAGE and MENUSLOT are
      *                both greater than zero and MENUSCHC is blank.

      *   MENUSSPECB - Special behaviors. May contain one or more of the
      *                following characters to specify special behaviors:
      *                  C - Confirm perform option.
      *                      If specified, the user must first confirm before
      *                      a chosen option is actually performed.
      *                  H - Use command handler.
      *                      If specified, command handler program CAPPSSAMCH
      *                      is executed to execute the command.
      *                Special behaviors C and H can be specified for the menu
      *                (MENUSPAGE=0), a page (MENUSPAGE>0 and MENUSLOT=0), or
      *                an option, but not for a header. If specified for a page,
      *                it has the same effect as if it was specified for all
      *                options on that page. If specified for the menu, it has
      *                the same effect as if it was specified for all pages.

      *   MENUSCMD   - Command to be executed when an option is performed.
      *                Can not be blank if MENUSPAGE and MENUSLOT are both
      *                greater than 0, and MENUSCHC is not blank.
      *                Is optional if MENUSPAGE or MENUSLOT is 0.
      *                Must be blank in all other cases.
      *                If this command is specified for the menu (MENUSPAGE=0)
      *                then it is executed every time an option is performed,
      *                before any other command.
      *                If this command is specified for a page (MENUSPAGE>0
      *                and MENUSLOT=0) then it is executed every time an option
      *                on page MENUSPAGE is performed, before executing the
      *                command specified for the option.
      *                If this command is specified for an option (MENUSPAGE>0
      *                and MENUSLOT>0) then it is executed when the option con-
      *                tained in slot MENUSLOT on page MENUSPAGE is performed,
      *                after executing the commands specified for the menu or
      *                the page.


     A                                      REF(CAPPSSAMFR)
     A                                      UNIQUE
     A          R MENUSREC
     A            MENUSBIN  R               REFFLD(TAM_MBIN)
     A            MENUSPAGE R               REFFLD(TAM_MPAGE)
     A            MENUSLOT  R               REFFLD(TAM_MSLOT)
     A            MENUSCHC  R               REFFLD(TAM_MCHC)
     A            MENUSTEXT R               REFFLD(TAM_MTEXT)
     A            MENUSSPECBR               REFFLD(TAM_SPECB)
     A            MENUSCMD  R               REFFLD(TAM_CMD)
     A          K MENUSBIN
     A          K MENUSPAGE
     A          K MENUSLOT

