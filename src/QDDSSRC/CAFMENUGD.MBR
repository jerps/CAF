
      *  (c) 2019 John Erps


      *  Common App - Menu Groups: Detail


      *  Indicator usage:

      *   Indicators 31-69 are reserved to highlight a screen or subfile
      *   field in error. See array "FEMap" in the program, which specifies
      *   a mapping between field/attribute names and error indicators.

      *   80 - If *on, the group is "special" and it's attributes can not
      *        be edited.

      *   10 - DSPMOD(*WIDE)
      *   21 - SFLDSPCTL
      *   22 - SFLDSP
      *   23 - SFLCLR
      *   27 - Set cursor location (CSRLOC)
      *   29 - SFLEND
      *   30 - Global error indicator
      *   74 - Key fields are editable (add/copy mode)
      *   75 - Non-key fields are editable (add/update/copy mode)
      *   76 - Non-key fields are not displayed (data not found)
      *   89 - Disable (non-dislay / protect) subfile record


     A                                      REF(*LIBL/CAPPSSAMFR)
     A                                      DSPSIZ(27 132 *WIDE 24 80 *NORMAL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      CF21
     A                                      CF22
     A                                      CF23
     A                                      CF24
     A                                      CLEAR
     A                                      HELP
     A                                      PAGEUP
     A                                      PAGEDOWN
     A                                      PRINT

      * Screen 1: Subfile Record
     A          R DFSCREEN1S                SFL
     A            D_GMEMBER     16   B 12  2
     A  75                                  DSPATR(UL)
     A N75                                  DSPATR(PR)
     A  76                                  DSPATR(ND)
     A            D_GMTEXT      50   O 12 21
     A  76                                  DSPATR(ND)
     A            D__GMBRT  R        H      REFFLD(TAM_MBRT)
     A            D__GMBRN  R        H      REFFLD(TAM_MBRN)
     A          R DFSCREEN1                 SFLCTL(DFSCREEN1S)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A  *NORMAL                             SFLSIZ(0009)
     A  *NORMAL                             SFLPAG(0008)
     A N10                                  DSPMOD(*NORMAL)
     A                                      OVERLAY
     A                                      RTNCSRLOC(*RECNAME &D__CURSREC
     A                                                &D__CURSFLD &D__CURSPOS)
     A  27                                  CSRLOC(D__CURSROW D__CURSCOL)
     A  21                                  SFLDSPCTL
     A  22                                  SFLDSP
     A  23                                  SFLCLR
     A  29                                  SFLEND
     A            D__CURREC      4S 0H      SFLRCDNBR
     A            D__CURSREC    10   H
     A            D__CURSFLD    10   H
     A            D__CURSPOS     4S 0H
     A            D__CURSROW     3  0H
     A            D__CURSCOL     3  0H
     A                                  4  2'Group . . . . . . . . . . . . . .:'
     A                                      DSPATR(HI)
     A            D_GROUP   R        B  4 37REFFLD(TAM_GROUP)
     A N74
     AO   N31                               DSPATR(HI)
     A  74N31                               DSPATR(UL)
     A  74 31                               DSPATR(RI PC)
     A N74                                  DSPATR(PR)
     A                                  6  2'Description . . . . . . . . . . .:'
     A N76        D_GTEXT   R        B  6 37REFFLD(TAM_TEXT)
     A                                      CHECK(LC)
     A  75N32N80                            DSPATR(UL)
     A  75 32N80                            DSPATR(RI PC)
     A N75
     AO       80                            DSPATR(PR)
     A                                  8  2'Active? . . . . . . . . . . . . .:'
     A N76        D_GACT    R        B  8 37REFFLD(TAM_ACTIVE)
     A  75N33N80                            DSPATR(UL)
     A  75 33N80                            DSPATR(RI PC)
     A N75
     AO       80                            DSPATR(PR)
     A  75N76N80                        8 60'Y, N'
     A                                 10  2'Members'
     A    N55                               DSPATR(HI)
     A     55                               DSPATR(RI)
     A            D_MLISTTXT    50   O 10 10

      * Header
     A          R DFHEADER
     A N10                                  DSPMOD(*NORMAL)
     A            D__HDPGMI     13   O  1  2
     A            D__HDTXT1     34   O  1 50DSPATR(HI)
     A  *NORMAL                         1 24
     A            D__HDDATE      8   O  1115
     A  *NORMAL                         1 63
     A            D__HDTIME      8   O  1124
     A  *NORMAL                         1 72
     A            D__HDUSRI     21   O  2  2
     A            D__HDTXT2     34   O  2 50
     A  *NORMAL                         2 24
     A            D__HDJOBI     17   O  2115
     A  *NORMAL                         2 63

      * Footer (*WIDE)
     A          R DFFOOTERW
     A                                      OVERLAY
     A            D__SCRIND      3   O 24130
     A                                      DSPATR(HI)
     A N30        D__FTMSG1W   130   O 25  2DSPATR(HI UL)
     A  30                             25  2'>>'
     A                                      DSPATR(RI)
     A                                      COLOR(RED)
     A  30        D__FTMSG2W   127   O 25  5DSPATR(HI UL)
     A            D__FTTEXTW   263   O 26  2

      * Footer (*NORMAL)
     A          R DFFOOTER
     A                                      DSPMOD(*NORMAL)
     A                                      OVERLAY
     A            D__SCRIND      3   O 21 78
     A                                      DSPATR(HI)
     A N30        D__FTMSG1     78   O 22  2DSPATR(HI UL)
     A  30                             22  2'>>'
     A                                      DSPATR(RI)
     A                                      COLOR(RED)
     A  30        D__FTMSG2     75   O 22  5DSPATR(HI UL)
     A            D__FTTEXT    159   O 23  2

