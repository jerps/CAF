
/*                                                                           +
     (c) 2019 John Erps                                                      +
     This software is licensed under the MIT license (see LICENSE)           +
                                                                             +
*/                                                                           +

             PGM        PARM(&CAFLIB &OWNER &PUBAUTNPL &PUBAUTPL +
                          &USRAUTNPL &USRAUTPL &AUTLNPL &AUTLPL +
                          &INCLMN &INCLSF &MNUADMAUT)
             DCL        VAR(&CAFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OWNER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PUBAUTNPL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PUBAUTPL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&AUTLNPL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&AUTLPL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&USRAUTNPL) TYPE(*CHAR) LEN(30)
             DCL        VAR(&USRAUTPL) TYPE(*CHAR) LEN(30)
             DCL        VAR(&INCLMN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INCLSF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MNUADMAUT) TYPE(*CHAR) LEN(20)

             DCL        VAR(&USRAUTNPL1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRAUTNPL2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRAUTNPL3) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRAUTPL1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRAUTPL2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRAUTPL3) TYPE(*CHAR) LEN(10)
             DCL        VAR(&AUTLNPL1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&AUTLNPL2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&AUTLPL1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&AUTLPL2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MNUADMAUT1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MNUADMAUT2) TYPE(*CHAR) LEN(10)

             DCL        VAR(&NUM1) TYPE(*DEC) LEN(4 0) VALUE(0)
             DCL        VAR(&NUM2) TYPE(*DEC) LEN(5 0) VALUE(0)
             DCL        VAR(&NUM3) TYPE(*DEC) LEN(5 0) VALUE(0)
             DCL        VAR(&NUM1A) TYPE(*CHAR) LEN(4) VALUE('????')
             DCL        VAR(&NUM2A) TYPE(*CHAR) LEN(5) VALUE('?????')
             DCL        VAR(&NUM3A) TYPE(*CHAR) LEN(5) VALUE('?????')

             DCL        VAR(&ERRMSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ERRMSGDTA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&ERRMSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRMSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRORSW) TYPE(*LGL)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))


             CHGVAR     VAR(&USRAUTNPL1) VALUE(%SST(&USRAUTNPL 1 10))
             CHGVAR     VAR(&USRAUTNPL2) VALUE(%SST(&USRAUTNPL 11 10))
             CHGVAR     VAR(&USRAUTNPL3) VALUE(%SST(&USRAUTNPL 21 10))
             CHGVAR     VAR(&USRAUTPL1) VALUE(%SST(&USRAUTPL 1 10))
             CHGVAR     VAR(&USRAUTPL2) VALUE(%SST(&USRAUTPL 11 10))
             CHGVAR     VAR(&USRAUTPL3) VALUE(%SST(&USRAUTPL 21 10))
             CHGVAR     VAR(&AUTLNPL1) VALUE(%SST(&AUTLNPL 1 10))
             CHGVAR     VAR(&AUTLNPL2) VALUE(%SST(&AUTLNPL 11 10))
             CHGVAR     VAR(&AUTLPL1) VALUE(%SST(&AUTLPL 1 10))
             CHGVAR     VAR(&AUTLPL2) VALUE(%SST(&AUTLPL 11 10))
             CHGVAR     VAR(&MNUADMAUT1) VALUE(%SST(&MNUADMAUT 1 10))
             CHGVAR     VAR(&MNUADMAUT2) VALUE(%SST(&MNUADMAUT 11 10))

             CALL       PGM(RTVDFTCAFL) PARM(&CAFLIB)

             DLTF       FILE(QTEMP/SETCAFAUT0)
             MONMSG     MSGID(CPF0000)
             DSPOBJD    OBJ(&CAFLIB/*ALL) OBJTYPE(*ALL) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/SETCAFAUT0)

             IF         COND(&OWNER *NE *SAME) THEN(CALL +
                          PGM(&CAFLIB/SETCAFAUT1) PARM(&CAFLIB +
                          &OWNER &INCLMN &INCLSF &NUM1 &NUM2 &NUM3))

             IF         COND(&PUBAUTNPL *NE *SAME *OR &PUBAUTPL *NE +
                          *SAME) THEN(CALL PGM(&CAFLIB/SETCAFAUT2) +
                          PARM(&CAFLIB &PUBAUTNPL &PUBAUTPL &INCLMN +
                          &INCLSF &NUM1 &NUM2 &NUM3))

             IF         COND(&USRAUTNPL1 *NE *SAME *OR &USRAUTPL1 +
                          *NE *SAME) THEN(CALL +
                          PGM(&CAFLIB/SETCAFAUT3) PARM(&CAFLIB +
                          &USRAUTNPL1 &USRAUTNPL2 &USRAUTNPL3 +
                          &USRAUTPL1 &USRAUTPL2 &USRAUTPL3 &INCLMN +
                          &INCLSF &NUM1 &NUM2 &NUM3))

             IF         COND(&AUTLNPL1 *NE *SAME *OR &AUTLPL1 *NE +
                          *SAME) THEN(CALL PGM(&CAFLIB/SETCAFAUT4) +
                          PARM(&CAFLIB &AUTLNPL1 &AUTLNPL2 &AUTLPL1 +
                          &AUTLPL2 &INCLMN &INCLSF &NUM1 &NUM2 &NUM3))

             IF         COND(&MNUADMAUT1 *NE *SAME) THEN(CALL +
                          PGM(&CAFLIB/SETCAFAUT5) PARM(&CAFLIB +
                          &MNUADMAUT1 &MNUADMAUT2 &NUM1 &NUM2 &NUM3))

             DLTF       FILE(QTEMP/SETCAFAUT0)
             MONMSG     MSGID(CPF0000)

             CALL       PGM(SETCAFAUT8) PARM(' ' &NUM1 &NUM2 &NUM1A +
                          &NUM2A &NUM3 &NUM3A)
             SNDPGMMSG  MSG('Performed ' *CAT &NUM2A *BCAT 'auth. +
                          operations on ' *CAT &NUM1A *BCAT +
                          'objects (' *CAT &NUM3A *BCAT 'errors).') +
                          MSGTYPE(*COMP)

             RETURN


 ERROR:      IF         COND(&ERRORSW) THEN(SNDPGMMSG MSGID(CPF9999) +
                          MSGF(QCPFMSG) MSGTYPE(*ESCAPE))
             CHGVAR     VAR(&ERRORSW) VALUE('1')
 ERROR2:     RCVMSG     MSGTYPE(*DIAG) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             IF         COND(&ERRMSGID = ' ') THEN(GOTO CMDLBL(ERROR3))
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*DIAG)
             GOTO       CMDLBL(ERROR2)
 ERROR3:     RCVMSG     MSGTYPE(*EXCP) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*ESCAPE)

             ENDPGM

