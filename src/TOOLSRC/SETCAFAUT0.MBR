
/*                                                                           +
     (c) 2019 John Erps                                                      +
     This software is licensed under the MIT license (see LICENSE)           +
                                                                             +
*/                                                                           +

             PGM        PARM(&CAFLIB &OWNER &AUTLNF &AUTLNFGR +
                          &AUTLPFLF &AUTLPFLFGR &PUBAUTNF +
                          &PUBAUTPFLF &INCLMDAUT &INCLSPFAUT +
                          &MNUADM &MNUADMGR)
             DCL        VAR(&CAFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OWNER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&AUTLNF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&AUTLNFGR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&AUTLPFLF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&AUTLPFLFGR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PUBAUTNF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PUBAUTPFLF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INCLMDAUT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INCLSPFAUT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MNUADM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MNUADMGR) TYPE(*CHAR) LEN(10)

             DCL        VAR(&NUM1) TYPE(*DEC) LEN(4 0) VALUE(0)
             DCL        VAR(&NUM2) TYPE(*DEC) LEN(5 0) VALUE(0)
             DCL        VAR(&NUM1A) TYPE(*CHAR) LEN(4) VALUE('????')
             DCL        VAR(&NUM2A) TYPE(*CHAR) LEN(5) VALUE('?????')

             DCL        VAR(&ERRMSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ERRMSGDTA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&ERRMSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRMSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRORSW) TYPE(*LGL)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))


             CALL       PGM(RTVDFTCAFL) PARM(&CAFLIB)

             DLTF       FILE(QTEMP/SETCAFAUT0)
             MONMSG     MSGID(CPF0000)
             DSPOBJD    OBJ(&CAFLIB/*ALL) OBJTYPE(*ALL) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/SETCAFAUT0)

             IF         COND(&OWNER *NE *SAME) THEN(CALL +
                          PGM(&CAFLIB/SETCAFAUT1) PARM(&CAFLIB +
                          &OWNER &NUM1 &NUM2))

             IF         COND(&AUTLNF *NE *SAME *OR &AUTLPFLF *NE +
                          *SAME) THEN(CALL PGM(&CAFLIB/SETCAFAUT2) +
                          PARM(&CAFLIB &AUTLNF &AUTLNFGR &AUTLPFLF +
                          &AUTLPFLFGR &INCLMDAUT &INCLSPFAUT &NUM1 +
                          &NUM2))

             IF         COND(&PUBAUTNF *NE *SAME *OR &PUBAUTPFLF *NE +
                          *SAME) THEN(CALL PGM(&CAFLIB/SETCAFAUT3) +
                          PARM(&CAFLIB &PUBAUTNF &PUBAUTPFLF +
                          &INCLMDAUT &INCLSPFAUT &NUM1 &NUM2))

             IF         COND(&MNUADM *NE *SAME) THEN(CALL +
                          PGM(&CAFLIB/SETCAFAUT4) PARM(&CAFLIB +
                          &MNUADM &MNUADMGR &NUM1 &NUM2))

             DLTF       FILE(QTEMP/SETCAFAUT0)
             MONMSG     MSGID(CPF0000)

             CALL       PGM(SETCAFAUT8) PARM(' ' &NUM1 &NUM2 &NUM1A +
                          &NUM2A)
             SNDPGMMSG  MSG('Performed ' *CAT &NUM2A *BCAT +
                          'authorization operations on ' *CAT +
                          &NUM1A *BCAT 'objects.') MSGTYPE(*COMP)

             RETURN


 ERROR:      IF         COND(&ERRORSW) THEN(SNDPGMMSG MSGID(CPF9999) +
                          MSGF(QCPFMSG) MSGTYPE(*ESCAPE))
             CHGVAR     VAR(&ERRORSW) VALUE('1')
 ERROR2:     RCVMSG     MSGTYPE(*DIAG) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             IF         COND(&ERRMSGID = ' ') THEN(GOTO CMDLBL(ERROR3))
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*DIAG)
             GOTO       CMDLBL(ERROR2)
 ERROR3:     RCVMSG     MSGTYPE(*EXCP) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*ESCAPE)

             ENDPGM

