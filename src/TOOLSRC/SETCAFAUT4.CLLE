
/*                                                                           +
     (c) 2019 John Erps                                                      +
     This software is licensed under the MIT license (see LICENSE)           +
                                                                             +
*/                                                                           +

             PGM        PARM(&CAFLIB &MNUADM &MNUADMGR &NUM1 &NUM2)
             DCL        VAR(&CAFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MNUADM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MNUADMGR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NUM1) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&NUM2) TYPE(*DEC) LEN(5 0)

             DCL        VAR(&ADD) TYPE(*CHAR) LEN(1) VALUE('0')

             DCLF       FILE(QTEMP/SETCAFAUT0)

             DCL        VAR(&ERRMSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ERRMSGDTA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&ERRMSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRMSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRORSW) TYPE(*LGL)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))


 READ:       RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(ENDREAD))
             IF         COND(&ODOBTP *EQ *FILE *AND %SST(&ODOBNM 1 +
                          9) *EQ CAPPSSAMF *OR &ODOBTP *EQ *DTAARA +
                          *AND %SST(&ODOBNM 1 9) *EQ CAPPSSAMD) +
                          THEN(DO)
             IF         COND(&MNUADMGR *EQ *GRT) THEN(GRTOBJAUT +
                          OBJ(&ODLBNM/&ODOBNM) OBJTYPE(&ODOBTP) +
                          USER(&MNUADM) AUT(*CHANGE))
             ELSE       CMD(RVKOBJAUT OBJ(&ODLBNM/&ODOBNM) +
                          OBJTYPE(&ODOBTP) USER(&MNUADM) AUT(*CHANGE))
             CALL       PGM(SETCAFAUT8) PARM(&ODOBNM &NUM1 &NUM2)
             ENDDO
             GOTO       CMDLBL(READ)
 ENDREAD:
             IF         COND(&MNUADMGR *EQ *GRT) THEN(CHGVAR +
                          VAR(&ADD) VALUE('1'))
             CALL       PGM(&CAFLIB/CAPPSSAMP1) PARM(*ADMIN &MNUADM +
                          &ADD)

             RETURN


 ERROR:      IF         COND(&ERRORSW) THEN(SNDPGMMSG MSGID(CPF9999) +
                          MSGF(QCPFMSG) MSGTYPE(*ESCAPE))
             CHGVAR     VAR(&ERRORSW) VALUE('1')
 ERROR2:     RCVMSG     MSGTYPE(*DIAG) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             IF         COND(&ERRMSGID = ' ') THEN(GOTO CMDLBL(ERROR3))
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*DIAG)
             GOTO       CMDLBL(ERROR2)
 ERROR3:     RCVMSG     MSGTYPE(*EXCP) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*ESCAPE)

             ENDPGM

