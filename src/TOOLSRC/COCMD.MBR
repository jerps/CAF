
/*                                                                           +
     (c) 2019 John Erps                                                      +
     This software is licensed under the MIT license (see LICENSE)           +
                                                                             +
                                                                             +
     Compile Command                                                         +
                                                                             +
     If &CMDPNAME is blank then the CP name defaults to &SRCMBR.             +
     If &CMDPLIB  is blank then the CP lib  defaults to *LIBL.               +
                                                                             +
*/

             PGM        PARM(&SRCLIB &SRCFILE &SRCMBR &TGTLIB +
                          &CMDPNAME &CMDPLIB)
             DCL        VAR(&SRCLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SRCFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SRCMBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TGTLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CMDPNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CMDPLIB) TYPE(*CHAR) LEN(10)

             DCL        VAR(&CPN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CPL) TYPE(*CHAR) LEN(10)

             DCL        VAR(&ERRMSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ERRMSGDTA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&ERRMSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRMSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRORSW) TYPE(*LGL)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))


             IF         COND(&CMDPNAME *EQ ' ') THEN(CHGVAR +
                          VAR(&CPN) VALUE(&SRCMBR))
             ELSE       CMD(CHGVAR VAR(&CPN) VALUE(&CMDPNAME))
             IF         COND(&CMDPLIB *EQ ' ') THEN(CHGVAR VAR(&CPL) +
                          VALUE(*LIBL))
             ELSE       CMD(CHGVAR VAR(&CPL) VALUE(&CMDPLIB))
             IF         COND(&CPL *EQ *LIBL) THEN(CRTCMD +
                          CMD(&TGTLIB/&SRCMBR) PGM(&CPN) +
                          SRCFILE(&SRCLIB/&SRCFILE) REPLACE(*YES))
             ELSE       CMD(CRTCMD CMD(&TGTLIB/&SRCMBR) +
                          PGM(&CPL/&CPN) SRCFILE(&SRCLIB/&SRCFILE) +
                          REPLACE(*YES))

             RETURN


 ERROR:      IF         COND(&ERRORSW) THEN(SNDPGMMSG MSGID(CPF9999) +
                          MSGF(QCPFMSG) MSGTYPE(*ESCAPE))
             CHGVAR     VAR(&ERRORSW) VALUE('1')
 ERROR2:     RCVMSG     MSGTYPE(*DIAG) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             IF         COND(&ERRMSGID = ' ') THEN(GOTO CMDLBL(ERROR3))
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*DIAG)
             GOTO       CMDLBL(ERROR2)
 ERROR3:     RCVMSG     MSGTYPE(*EXCP) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*ESCAPE)

             ENDPGM

