
       // Process Source Compile Commands

       // Parameter 1: IN - Source type:
       //                     RPG
       //                     RPGLE
       //                     other: not supported; exit
       //           2: IN - Source library
       //           3: IN - Source file
       //           4: IN - Source member
       //           5: IN - Command selector
       //                     Selects the commands in the source to be pro-
       //                     cessed with QCMDEXC. Each source line can con-
       //                     tain at most one command to process. A command
       //                     can not continue on the next line. The line must
       //                     be a comment line without a specification type,
       //                     i.e. it must start with at least 6 spaces which
       //                     are immediately followed with either "*$X" (RPG
       //                     or RPGLE) or "//$X". The X denotes the command
       //                     selector, which can be one of the following.
       //                       B: "before" commands
       //                       A: "after" commands


     h dftactgrp(*NO) actgrp(*NEW)


     fSOURCE    if   f  200        disk    extfile('QTEMP/COPSCCSRC')
     f                                     usropn


     d Main            pr                  extpgm('COPSCC')
     d   srctype                     10    const
     d   srclib                      10    const
     d   srcfile                     10    const
     d   srcmbr                      10    const
     d   cmdsel                       1    const

     d ExecCmd         pr
     d   cmd                       5000    const


     d Main            pi
     d   srctype                     10    const
     d   srclib                      10    const
     d   srcfile                     10    const
     d   srcmbr                      10    const
     d   cmdsel                       1    const

     d srcl            ds           200
     d cmd             s            200


      /free


       *inlR = *on;

       if     srctype <> 'RPG'
          and srctype <> 'RPGLE';
         return;
       endif;

       monitor;
         ExecCmd('DLTF FILE(QTEMP/COPSCCSRC)');
       on-error;
       endmon;

       ExecCmd('CRTPF FILE(QTEMP/COPSCCSRC) RCDLEN(200)');

       ExecCmd(
         'CPYF ' +
           'FROMFILE(' + %trim(srclib) + '/' + %trim(srcfile) + ') ' +
           'TOFILE(QTEMP/COPSCCSRC) ' +
           'FROMMBR(' + %trim(srcmbr) + ') ' +
           'MBROPT(*REPLACE) ' +
           'FMTOPT(*CVTSRC)');

       open SOURCE;

       read SOURCE srcl;
       dow     not %eof
           and %subst(srcl:1:6) = '';

         if srcl <> '';

           srcl = %triml(srcl);

           select;
           when     (   srctype = 'RPG'
                     or srctype = 'RPGLE')
                and %subst(srcl:1:1) = '*';
             srcl = %subst(srcl:2);
           when     srctype = 'RPGLE'
                and %subst(srcl:1:2) = '//';
             srcl = %subst(srcl:3);
           other;
             leave;
           endsl;

           if     %subst(srcl:1:1) = '$'
              and %subst(srcl:2:1) = cmdsel;
             ExecCmd(%subst(srcl:3));
           endif;

         endif;

         read SOURCE srcl;
       enddo;

       close SOURCE;

       monitor;
         ExecCmd('DLTF FILE(QTEMP/COPSCCSRC)');
       on-error;
       endmon;

       return;


      /end-free


     p ExecCmd         b
     d ExecCmd         pi
     d   cmd                       5000    const

     d CallQCMDEXC     pr                  extpgm('QCMDEXC')
     d   cmd                       5000    const
     d   cmdlen                      15  5 const

      /free

       CallQCMDEXC(%trim(cmd):%len(%trim(cmd)));

      /end-free
     p                 e

