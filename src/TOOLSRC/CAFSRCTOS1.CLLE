
/*                                                                           +
     (c) 2019 John Erps                                                      +
     This software is licensed under the MIT license (see LICENSE)           +
                                                                             +
*/                                                                           +

             PGM        PARM(&CAFLIB &SRCFILE &STMFPATH)
             DCL        VAR(&CAFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SRCFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&STMFPATH) TYPE(*CHAR) LEN(150)

             DCL        VAR(&SRCTYPE) TYPE(*CHAR) LEN(10)

             DCLF       FILE(QTEMP/CAFSRCTOS1)

             DCL        VAR(&ERRMSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ERRMSGDTA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&ERRMSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRMSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRORSW) TYPE(*LGL)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))


             DLTF       FILE(QTEMP/CAFSRCTOS1)
             MONMSG     MSGID(CPF0000)
             DSPFD      FILE(&CAFLIB/&SRCFILE) TYPE(*MBRLIST) +
                          OUTPUT(*OUTFILE) FILEATR(*PF) +
                          OUTFILE(QTEMP/CAFSRCTOS1)

 READ:       RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(ENDREAD))

             RTVMBRD    FILE(&CAFLIB/&SRCFILE) MBR(&MLNAME) +
                          SRCTYPE(&SRCTYPE)
             CPYTOSTMF  FROMMBR('/QSYS.LIB/' *CAT &CAFLIB *TCAT +
                          '.LIB/' *CAT &SRCFILE *TCAT '.FILE/' *CAT +
                          &MLNAME *TCAT '.MBR') TOSTMF(&STMFPATH +
                          *TCAT '/' *CAT &MLNAME *TCAT '.' *CAT +
                          &SRCTYPE) STMFOPT(*REPLACE) +
                          STMFCCSID(*PCASCII)

             GOTO       CMDLBL(READ)

 ENDREAD:
             DLTF       FILE(QTEMP/CAFSRCTOS1)
             MONMSG     MSGID(CPF0000)

             RETURN


 ERROR:      IF         COND(&ERRORSW) THEN(SNDPGMMSG MSGID(CPF9999) +
                          MSGF(QCPFMSG) MSGTYPE(*ESCAPE))
             CHGVAR     VAR(&ERRORSW) VALUE('1')
 ERROR2:     RCVMSG     MSGTYPE(*DIAG) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             IF         COND(&ERRMSGID = ' ') THEN(GOTO CMDLBL(ERROR3))
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*DIAG)
             GOTO       CMDLBL(ERROR2)
 ERROR3:     RCVMSG     MSGTYPE(*EXCP) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*ESCAPE)

             ENDPGM

