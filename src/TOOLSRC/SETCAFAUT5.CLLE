
/*                                                                           +
     (c) 2019 John Erps                                                      +
     This software is licensed under the MIT license (see LICENSE)           +
                                                                             +
*/                                                                           +

             PGM        PARM(&CAFLIB &MNUADMAUT1 &MNUADMAUT2 &NUM1 +
                          &NUM2 &NUM3)
             DCL        VAR(&CAFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MNUADMAUT1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MNUADMAUT2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NUM1) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&NUM2) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&NUM3) TYPE(*DEC) LEN(5 0)

             DCL        VAR(&ADD) TYPE(*CHAR) LEN(1) VALUE('0')
             DCL        VAR(&OBJN) TYPE(*CHAR) LEN(20)

             DCLF       FILE(QTEMP/SETCAFAUT0)

             DCL        VAR(&ERRMSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ERRMSGDTA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&ERRMSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRMSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRORSW) TYPE(*LGL)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))


 READ:       RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(ENDREAD))
             CHGVAR     VAR(&OBJN) VALUE(&ODOBNM *CAT &ODOBTP)
             IF         COND(&ODOBTP *EQ *FILE *AND %SST(&ODOBNM 1 +
                          9) *EQ CAPPSSAMF *OR &ODOBTP *EQ *DTAARA +
                          *AND %SST(&ODOBNM 1 9) *EQ CAPPSSAMA) +
                          THEN(DO)
             IF         COND(&MNUADMAUT2 *EQ *GRT) THEN(DO)
             GRTOBJAUT  OBJ(&ODLBNM/&ODOBNM) OBJTYPE(&ODOBTP) +
                          USER(&MNUADMAUT1) AUT(*CHANGE)
             MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&NUM3) +
                          VALUE(&NUM3 + 1))
             ENDDO
             ELSE       CMD(DO)
             RVKOBJAUT  OBJ(&ODLBNM/&ODOBNM) OBJTYPE(&ODOBTP) +
                          USER(&MNUADMAUT1) AUT(*CHANGE)
             MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&NUM3) +
                          VALUE(&NUM3 + 1))
             ENDDO
             CALL       PGM(SETCAFAUT8) PARM(&OBJN &NUM1 &NUM2)
             ENDDO
             GOTO       CMDLBL(READ)
 ENDREAD:
             IF         COND(&MNUADMAUT2 *EQ *GRT) THEN(CHGVAR +
                          VAR(&ADD) VALUE('1'))
             CALL       PGM(&CAFLIB/CAPPSSAMP1) PARM(*ADMIN +
                          &MNUADMAUT1 &ADD)

             RETURN


 ERROR:      IF         COND(&ERRORSW) THEN(SNDPGMMSG MSGID(CPF9999) +
                          MSGF(QCPFMSG) MSGTYPE(*ESCAPE))
             CHGVAR     VAR(&ERRORSW) VALUE('1')
 ERROR2:     RCVMSG     MSGTYPE(*DIAG) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             IF         COND(&ERRMSGID = ' ') THEN(GOTO CMDLBL(ERROR3))
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*DIAG)
             GOTO       CMDLBL(ERROR2)
 ERROR3:     RCVMSG     MSGTYPE(*EXCP) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*ESCAPE)

             ENDPGM

