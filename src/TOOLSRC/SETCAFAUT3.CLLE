
/*                                                                           +
     (c) 2019 John Erps                                                      +
     This software is licensed under the MIT license (see LICENSE)           +
                                                                             +
*/                                                                           +

             PGM        PARM(&CAFLIB &USRAUTNPL1 &USRAUTNPL2 +
                          &USRAUTNPL3 &USRAUTPL1 &USRAUTPL2 +
                          &USRAUTPL3 &INCLMN &INCLSF &NUM1 &NUM2 &NUM3)
             DCL        VAR(&CAFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRAUTNPL1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRAUTNPL2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRAUTNPL3) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRAUTPL1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRAUTPL2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRAUTPL3) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INCLMN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INCLSF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NUM1) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&NUM2) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&NUM3) TYPE(*DEC) LEN(5 0)

             DCL        VAR(&MN) TYPE(*LGL)
             DCL        VAR(&SF) TYPE(*LGL)
             DCL        VAR(&PL) TYPE(*LGL)
             DCL        VAR(&F) TYPE(*CHAR) LEN(1)
             DCL        VAR(&OBJN) TYPE(*CHAR) LEN(20)

             DCLF       FILE(QTEMP/SETCAFAUT0)

             DCL        VAR(&ERRMSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ERRMSGDTA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&ERRMSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRMSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRORSW) TYPE(*LGL)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))


 READ:       RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(ENDREAD))
             CHGVAR     VAR(&OBJN) VALUE(&ODOBNM *CAT &ODOBTP)
             CHGVAR     VAR(&MN) VALUE(&ODOBTP *EQ *DTAARA *AND +
                          %SST(&ODOBNM 1 9) *EQ CAPPSSAMA *OR +
                          &ODOBTP *EQ *FILE *AND (&ODOBAT *EQ PF +
                          *OR &ODOBAT *EQ LF) *AND %SST(&ODOBNM 1 +
                          9) *EQ CAPPSSAMF)
             CHGVAR     VAR(&SF) VALUE(0 *EQ 1)
             IF         COND(&ODOBTP *EQ *FILE *AND &ODOBAT *EQ PF) +
                          THEN(DO)
             CALL       PGM(TIFCAFSRCF) PARM(&ODOBNM &ODLBNM &F)
             CHGVAR     VAR(&SF) VALUE(&F *EQ '1')
             ENDDO
             IF         COND(&MN *AND &INCLMN *NE *NO *AND &INCLSF +
                          *NE *ONLY *OR &SF *AND &INCLSF *NE *NO +
                          *AND &INCLMN *NE *ONLY *OR *NOT &MN *AND +
                          *NOT &SF *AND &INCLMN *NE *ONLY *AND +
                          &INCLSF *NE *ONLY) THEN(DO)
             CHGVAR     VAR(&PL) VALUE(&ODOBTP *EQ *FILE *AND +
                          (&ODOBAT *EQ PF *OR &ODOBAT *EQ LF))
             IF         COND(&USRAUTNPL1 *NE *SAME *AND *NOT &PL) +
                          THEN(DO)
             IF         COND(&USRAUTNPL3 *EQ *GRT) THEN(DO)
             GRTOBJAUT  OBJ(&ODLBNM/&ODOBNM) OBJTYPE(&ODOBTP) +
                          USER(&USRAUTNPL1) AUT(&USRAUTNPL2)
             MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&NUM3) +
                          VALUE(&NUM3 + 1))
             ENDDO
             ELSE       CMD(DO)
             RVKOBJAUT  OBJ(&ODLBNM/&ODOBNM) OBJTYPE(&ODOBTP) +
                          USER(&USRAUTNPL1) AUT(&USRAUTNPL2)
             MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&NUM3) +
                          VALUE(&NUM3 + 1))
             ENDDO
             CALL       PGM(SETCAFAUT8) PARM(&OBJN &NUM1 &NUM2)
             ENDDO
             IF         COND(&USRAUTPL1 *NE *SAME *AND &PL) THEN(DO)
             IF         COND(&USRAUTPL2 *EQ *GRT) THEN(DO)
             GRTOBJAUT  OBJ(&ODLBNM/&ODOBNM) OBJTYPE(&ODOBTP) +
                          USER(&USRAUTPL1) AUT(&USRAUTPL2)
             MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&NUM3) +
                          VALUE(&NUM3 + 1))
             ENDDO
             ELSE       CMD(DO)
             RVKOBJAUT  OBJ(&ODLBNM/&ODOBNM) OBJTYPE(&ODOBTP) +
                          USER(&USRAUTPL1) AUT(&USRAUTPL2)
             MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&NUM3) +
                          VALUE(&NUM3 + 1))
             ENDDO
             CALL       PGM(SETCAFAUT8) PARM(&OBJN &NUM1 &NUM2)
             ENDDO
             ENDDO
             GOTO       CMDLBL(READ)
 ENDREAD:
             RETURN


 ERROR:      IF         COND(&ERRORSW) THEN(SNDPGMMSG MSGID(CPF9999) +
                          MSGF(QCPFMSG) MSGTYPE(*ESCAPE))
             CHGVAR     VAR(&ERRORSW) VALUE('1')
 ERROR2:     RCVMSG     MSGTYPE(*DIAG) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             IF         COND(&ERRMSGID = ' ') THEN(GOTO CMDLBL(ERROR3))
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*DIAG)
             GOTO       CMDLBL(ERROR2)
 ERROR3:     RCVMSG     MSGTYPE(*EXCP) MSGDTA(&ERRMSGDTA) +
                          MSGID(&ERRMSGID) MSGF(&ERRMSGF) +
                          SNDMSGFLIB(&ERRMSGFLIB)
             SNDPGMMSG  MSGID(&ERRMSGID) MSGF(&ERRMSGFLIB/&ERRMSGF) +
                          MSGDTA(&ERRMSGDTA) MSGTYPE(*ESCAPE)

             ENDPGM

