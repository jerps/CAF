
/*                                                                           +
     (c) 2019 John Erps                                                      +
                                                                             +
     Compile Object - Cmd Processor                                          +
                                                                             +
*/
             PGM        PARM(&QSRCFILE &SRCMBR &TGTLIB &TGTOBJTYPE +
                          &ACTGRP &TGTRLS &INCDBGSRC &PARM1 &PARM2 +
                          &PARM3)
             DCL        VAR(&QSRCFILE) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SRCMBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TGTLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TGTOBJTYPE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ACTGRP) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TGTRLS) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INCDBGSRC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PARM1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PARM2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PARM3) TYPE(*CHAR) LEN(10)

             DCL        VAR(&SRCLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SRCFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SRCTYPE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PSCCA) TYPE(*LGL) VALUE('0')
             DCL        VAR(&SLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TGTRLS2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INCDBGSRC2) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&SRCFILE) VALUE(%SST(&QSRCFILE 1 10))
             CHGVAR     VAR(&SRCLIB) VALUE(%SST(&QSRCFILE 11 10))

             IF         COND(&TGTLIB *EQ *SRCLIB) THEN(DO)
             IF         COND(&SRCLIB *EQ *LIBL) THEN(SNDPGMMSG +
                          MSGID(CPF9897) MSGF(QCPFMSG) +
                          MSGDTA('Target library ' *CAT &TGTLIB +
                          *TCAT ' not allowed for source library ' +
                          *CAT &SRCLIB *TCAT '.') MSGTYPE(*ESCAPE))
             ELSE       CMD(CHGVAR VAR(&TGTLIB) VALUE(&SRCLIB))
             ENDDO

             CHGVAR     VAR(&TGTRLS2) VALUE(&TGTRLS)
             CHKOBJ     OBJ(QTEMP/CO@TGTRLS) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(NOTGTRLS))
             RTVDTAARA  DTAARA(QTEMP/CO@TGTRLS *ALL) RTNVAR(&TGTRLS2)
NOTGTRLS:
             CHGVAR     VAR(&INCDBGSRC2) VALUE(&INCDBGSRC)
             CHKOBJ     OBJ(QTEMP/CO@IDBGSRC) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(NOIDBGSRC))
             RTVDTAARA  DTAARA(QTEMP/CO@IDBGSRC *ALL) +
                          RTNVAR(&INCDBGSRC2)
NOIDBGSRC:

             RTVOBJD    OBJ(&SRCLIB/&SRCFILE) OBJTYPE(*FILE) +
                          RTNLIB(&SLIB)

             RTVMBRD    FILE(&SLIB/&SRCFILE) MBR(&SRCMBR) +
                          SRCTYPE(&SRCTYPE)

             SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) +
                          MSGDTA('Compiling from source ' *CAT +
                          &SRCMBR *TCAT ', type ' *CAT &SRCTYPE +
                          *TCAT ', file ' *CAT &SLIB *TCAT '/' *CAT +
                          &SRCFILE *TCAT ', tlib ' *CAT &TGTLIB +
                          *TCAT ' . . .') TOPGMQ(*EXT) MSGTYPE(*STATUS)

             /* Process source compile commands BEFORE */
             CALL       PGM(COPSCC) PARM(&SRCTYPE &SLIB &SRCFILE +
                          &SRCMBR B)
             CHGVAR     VAR(&PSCCA) VALUE('1')

             /* PF LF */
             IF         COND(&SRCTYPE *EQ PF *OR &SRCTYPE *EQ LF) +
                          THEN(DO)
             IF         COND(&TGTOBJTYPE *NE *DEFAULT) +
                          THEN(SNDPGMMSG MSGID(CPF9897) +
                          MSGF(QCPFMSG) MSGDTA('Target object type +
                          ' *CAT &TGTOBJTYPE *TCAT ' not valid for +
                          source type ' *CAT &SRCTYPE *TCAT '.') +
                          MSGTYPE(*ESCAPE))
             CALL       PGM(COPFLF) PARM(&SLIB &SRCFILE &SRCMBR +
                          &TGTLIB)
             ENDDO

             ELSE       CMD(DO)

             /* DSPF */
             IF         COND(&SRCTYPE *EQ DSPF) THEN(DO)
             IF         COND(&TGTOBJTYPE *NE *DEFAULT) +
                          THEN(SNDPGMMSG MSGID(CPF9897) +
                          MSGF(QCPFMSG) MSGDTA('Target object type +
                          ' *CAT &TGTOBJTYPE *TCAT ' not valid for +
                          source type ' *CAT &SRCTYPE *TCAT '.') +
                          MSGTYPE(*ESCAPE))
             CALL       PGM(CODSPF) PARM(&SLIB &SRCFILE &SRCMBR +
                          &TGTLIB)
             ENDDO

             ELSE       CMD(DO)

             /* CMD (uses &PARM1/&PARM2 for cmd processor name/lib) */
             IF         COND(&SRCTYPE *EQ CMD) THEN(DO)
             IF         COND(&TGTOBJTYPE *NE *DEFAULT) +
                          THEN(SNDPGMMSG MSGID(CPF9897) +
                          MSGF(QCPFMSG) MSGDTA('Target object type +
                          ' *CAT &TGTOBJTYPE *TCAT ' not valid for +
                          source type ' *CAT &SRCTYPE *TCAT '.') +
                          MSGTYPE(*ESCAPE))
             CALL       PGM(COCMD) PARM(&SLIB &SRCFILE &SRCMBR +
                          &TGTLIB &PARM1 &PARM2)
             ENDDO

             ELSE       CMD(DO)

             /* CLP CLLE */
             IF         COND(&SRCTYPE *EQ CLP *OR &SRCTYPE *EQ CLLE) +
                          THEN(DO)
             IF         COND(&TGTOBJTYPE *NE *DEFAULT *AND +
                          &TGTOBJTYPE *NE *MODULE) THEN(SNDPGMMSG +
                          MSGID(CPF9897) MSGF(QCPFMSG) +
                          MSGDTA('Target object type ' *CAT +
                          &TGTOBJTYPE *TCAT ' not valid for source +
                          type ' *CAT &SRCTYPE *TCAT '.') +
                          MSGTYPE(*ESCAPE))
             IF         COND(&TGTOBJTYPE *EQ *MODULE) THEN(CALL +
                          PGM(COCLMOD) PARM(&SLIB &SRCFILE &SRCMBR +
                          &TGTLIB &TGTRLS2 &INCDBGSRC2))
             ELSE       CMD(CALL PGM(COCLPGM) PARM(&SLIB &SRCFILE +
                          &SRCMBR &TGTLIB &ACTGRP &TGTRLS2 +
                          &INCDBGSRC2))
             ENDDO

             ELSE       CMD(DO)

             /* RPG RPGLE */
             IF         COND(&SRCTYPE *EQ RPG *OR &SRCTYPE *EQ +
                          RPGLE) THEN(DO)
             IF         COND(&TGTOBJTYPE *NE *DEFAULT *AND +
                          &TGTOBJTYPE *NE *MODULE) THEN(SNDPGMMSG +
                          MSGID(CPF9897) MSGF(QCPFMSG) +
                          MSGDTA('Target object type ' *CAT +
                          &TGTOBJTYPE *TCAT ' not valid for source +
                          type ' *CAT &SRCTYPE *TCAT '.') +
                          MSGTYPE(*ESCAPE))
             IF         COND(&TGTOBJTYPE *EQ *MODULE) THEN(CALL +
                          PGM(CORPGMOD) PARM(&SLIB &SRCFILE &SRCMBR +
                          &TGTLIB &TGTRLS2 &INCDBGSRC2))
             ELSE       CMD(CALL PGM(CORPGPGM) PARM(&SLIB &SRCFILE +
                          &SRCMBR &TGTLIB &ACTGRP &TGTRLS2 +
                          &INCDBGSRC2))
             ENDDO

             ELSE       CMD(DO)

             SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) MSGDTA('Source +
                          type ' *CAT &SRCTYPE *TCAT ' not +
                          recognized.') MSGTYPE(*ESCAPE)

             ENDDO
             ENDDO
             ENDDO
             ENDDO
             ENDDO

             /* Process source compile commands AFTER */
             CHGVAR     VAR(&PSCCA) VALUE('0')
             CALL       PGM(COPSCC) PARM(&SRCTYPE &SLIB &SRCFILE +
                          &SRCMBR A)

             SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) MSGDTA(' ') +
                          TOPGMQ(*EXT) MSGTYPE(*STATUS)

             ENDPGM

